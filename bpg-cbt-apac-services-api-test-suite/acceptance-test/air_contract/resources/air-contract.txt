*** Setting ***
Resource          ../../resources/api-imports.txt
Resource          ../../resources/api-utilities.txt
Resource          ../../resources/api-variables.txt

***Keywords***
Create Test Variables For Air Contract
    [Arguments]    ${country_code}=hk
    ${air_contract_list_data}    Split String    ${${country_code}_air_contract_list}    |
    ${air_contract_list_data_length}    Get Length    ${air_contract_list_data}
    Set Test Variable    ${air_contract_list_data}
    Set Test Variable    ${air_contract_list_data_length}
    ${index}    Set Variable    0
    : FOR    ${data}    IN    @{air_contract_list_data}
    \    ${data_item}    Split String    ${data}    ,
    \    Set Test Variable    ${client_acct_number_${index}_value}    ${data_item[0]}
    \    Set Test Variable    ${country_code_${index}_value}    ${data_item[1]}
    \    Set Test Variable    ${airline_code_${index}_value}    ${data_item[2]}
    \    Set Test Variable    ${fop_code_${data_item[0]}${data_item[1]}${data_item[2]}_value}    ${data_item[3]}
    \    ${index}    Evaluate    ${index}+1

Create Put Request Body
    [Arguments]    ${country_code}    ${airline_code}    ${client_acct_number}    ${fop_code}
    ${air_contract_request_list}    Create List
    Run Keyword If    "${airline_code}"!="null"    Append To List    ${air_contract_request_list}    "airlineCode":"${airline_code}"
    Run Keyword If    "${client_acct_number}"!="null"    Append To List    ${air_contract_request_list}    "clientAccountNumber":"${client_acct_number}"
    Run Keyword If    "${country_code}"!="null"    Append To List    ${air_contract_request_list}    "countryCode":"${country_code}"
    Run Keyword If    "${fop_code}"!="null"    Append To List    ${air_contract_request_list}    "fopCode":"${fop_code}"
    ${air_contract_data_list}    Set Variable    ${EMPTY}
    ${air_contract_request_list_length}    Get Length    ${air_contract_request_list}
    : FOR    ${i}    IN RANGE    0    ${air_contract_request_list_length}
    \    ${air_contract_data_list}    Set Variable If    ${i}==0    ${EMPTY}    ${air_contract_data_list},
    \    ${air_contract_data_list}    Set Variable    ${air_contract_data_list}${air_contract_request_list[${i}]}
    ${air_contract_data_list}    Set Variable If    "${air_contract_request_list_length}"=="0"    ${EMPTY}    ${air_contract_data_list}
    ${data}    Set Variable    {${air_contract_data_list}}
    Set Test Variable    ${data}
    Log    ${data}

Evict Air Contract Cache
    [Arguments]    ${number}=2    ${token}=${default_token}
    Create Session    air-contract    ${${env}_base_url}    verify=True
    ${headers}    Run Keyword If    "${token}" != "None"    Create Dictionary    Content-Type=application/json    Authorization=Bearer ${token}
    ...    ELSE    Create Dictionary    Content-Type=application/json
    : FOR    ${index}    IN RANGE    0    ${number}
    \    ${response}    Get Request    air-contract    /apac-services-rest/api/caches/evict/air-contracts    headers=${headers}
    \    Set Test Variable    ${response}
    \    Verify Response Status Code Is Correct    200
    \    Log    ${response.headers}
    \    Log    ${response.content}
    Set Test Variable    ${api_flag}    air-contract

Get Air Contract Id
    ${air_contract_id}    Get Json Value As String    ${response.content}    $.id
    Set Test Variable    ${air_contract_id}

Send Air Contract Delete Request
    [Arguments]    ${air_contract_id}=${air_contract_id}    ${token}=${default_token}
    Create Session    air-contract    ${${env}_base_url}    verify=True
    ${headers}    Run Keyword If    "${token}" != "None"    Create Dictionary    Content-Type=application/json    Authorization=Bearer ${token}
    ...    ELSE    Create Dictionary    Content-Type=application/json
    ${response}    Delete Request    air-contract    /apac-services-rest/api/air-contract/${air_contract_id}    headers=${headers}
    Set Test Variable    ${response}
    Log    ${response.headers}
    Log    ${response.content}
    Set Test Variable    ${api_flag}    air-contract
    
Send Air Contract Get Request
    [Arguments]    ${country_code}    ${airline_code}    ${client_acct_number}    ${token}=${default_token}
    Create Session    air-contract    ${${env}_base_url}    verify=True
    ${headers}    Run Keyword If    "${token}" != "None"    Create Dictionary    Content-Type=application/json    Authorization=Bearer ${token}
    ...    ELSE    Create Dictionary    Content-Type=application/json
    ${response}    Get Request    air-contract    apac-services-rest/api/air-contract/${country_code}/${airline_code}/${client_acct_number}    headers=${headers}
    Set Test Variable    ${response}
    Log    ${response.headers}
    Log    ${response.content}
    Set Test Variable    ${api_flag}    air-contract
    
Send Air Contract Put Request
    [Arguments]    ${country_code}    ${airline_code}    ${client_acct_number}    ${fop_code}    ${token}=${default_token}
    Create Session    air-contract    ${${env}_base_url}    verify=True
    ${headers}    Run Keyword If    "${token}" != "None"    Create Dictionary    Content-Type=application/json    Authorization=Bearer ${token}
    ...    ELSE    Create Dictionary    Content-Type=application/json
    Create Put Request Body    ${country_code}    ${airline_code}    ${client_acct_number}    ${fop_code} 
    ${response}    Put Request    air-contract    /apac-services-rest/api/air-contract    data=${data}    headers=${headers}
    Set Test Variable    ${response}
    Log    ${response.headers}
    Log    ${response.content}
    Set Test Variable    ${api_flag}    air-contract
    
Verify Air Contract Is Retrieved Correctly
    [Arguments]    ${defaul_fop_code}=${EMPTY}
    ${client_acct_number}    Get Json Value As String    ${response.content}    $.clientAccountNumber
    ${country_code}    Get Json Value As String    ${response.content}    $.countryCode
    ${airline_code}    Get Json Value As String    ${response.content}    $.airlineCode
    ${fop_code_value}    Set Variable If    "${defaul_fop_code}"=="${EMPTY}"    ${fop_code_${client_acct_number}${country_code}${airline_code}_value}    ${defaul_fop_code}    
    Verify Json Value Is Correct    $.fopCode    ${fop_code_value}
    
Verify Air Contracts Are Retrieved Correctly
    : FOR    ${index}    IN RANGE    ${air_contract_list_data_length}
    \    Send Air Contract Get Request    country_code=${country_code_${index}_value}    airline_code=${airline_code_${index}_value}    client_acct_number=${client_acct_number_${index}_value}
    \    Verify Response Status Code Is Correct    200
    \    Verify Air Contract Is Retrieved Correctly    
    
Verify Air Contract Record Is Not Retrieved
    Verify String Does Not Contain Substring    ${response.text}    "clientAccountNumber"
    Verify String Does Not Contain Substring    ${response.text}    "countryCode"
    Verify String Does Not Contain Substring    ${response.text}    "airlineCode"
    Verify String Does Not Contain Substring    ${response.text}    "fopCode"